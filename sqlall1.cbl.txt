000100CBL SQLIMS
000200 IDENTIFICATION DIVISION.
000300 PROGRAM-ID. 'SQLALL1'.
000400*
000500 ENVIRONMENT DIVISION.
000600 CONFIGURATION SECTION.
000700 SOURCE-COMPUTER. IBM-370.
000800 OBJECT-COMPUTER. IBM-370.
000900*
001000 DATA DIVISION.
001100 WORKING-STORAGE SECTION.
001200*
001300*****************************************************
001400* SQL INCLUDE FOR SQLCA *
001500*****************************************************
001600 EXEC SQLIMS
001700 INCLUDE SQLIMSCA
001800 END-EXEC.
001900*****************************************************
002000* SQL INCLUDE FOR SQLDA *
002100*****************************************************
002200 EXEC SQLIMS
002300 INCLUDE SQLIMSDA
002400 END-EXEC.
002500*****************************************************
002600* CUSTOMER INFORMATION *
002700*****************************************************
002800 01 SQL-CUSTROOT.
002900    05 SQL-CUSTOMERNUMBER PIC S9(11) BINARY.
003000    05 SQL-FIRSTNAME PIC X(10).
003100    05 SQL-LASTNAME PIC X(20).
003200    05 SQL-CUSTOMERDATA PIC X(67).
003300 01 SQL-STATEMENT.
003400    49 SQL-STMT-LENGTH PIC S9(4) COMP VALUE +200.
003500    49 SQL-STMT-TEXT PIC X(200) VALUE SPACES.
003600 01 DBD-COUNT PIC S9(3) COMP.
003700 01 LOOP-COUNT PIC S9(3) COMP.
003800 01 SQLIMSCODE-DISP PIC S9(5) DISPLAY.
003900*
004000 LINKAGE SECTION.
004100   01 IO-PCB.
004200      05 IO-PCB-LTERM PIC X(8).
004300      05 FILLER PIC X(2).
004400      05 IO-PCB-STATUS PIC X(2).
004500      05 IO-PCB-DATE PIC S9(7) COMP-3.
004600      05 IO-PCB-TIME PIC S9(7) COMP-3.
004700      05 IO-PCB-SEQ PIC X(4).
004800      05 IO-PCB-MODNAME PIC X(8).
004900      05 IO-PCB-USERID PIC X(8).
005000      05 IO-PCB-GROUP PIC X(8).
005100      05 IO-PCB-TSTAMP.
005200        10 IO-PCB-TS-DATE PIC S9(7) COMP-3.
005300        10 IO-PCB-TS-TIME PIC X(6).
005400        10 IO-PCB-TS-OFFSET PIC S9(3) COMP-3.
005500      05 IO-PCB-IND PIC X(1).
005600*
005700   01 ALT-PCB.
005800      05 ALT-PCB-LTERM PIC X(8).
005900      05 FILLER PIC X(2).
006000      05 ALT-PCB-STATUS PIC X(2).
006100      05 ALT-PCB-DATE PIC S9(7) COMP-3.
006200      05 ALT-PCB-TIME PIC S9(7) COMP-3.
006300      05 ALT-PCB-SEQ PIC X(4).
006400      05 ALT-PCB-MODNAME PIC X(8).
006500      05 ALT-PCB-USERID PIC X(8).
006600      05 ALT-PCB-GROUP PIC X(8).
006700      05 ALT-PCB-TSTAMP.
006800        10 ALT-PCB-TS-DATE PIC S9(7) COMP-3.
006900        10 ALT-PCB-TS-TIME PIC X(6).
007000        10 ALT-PCB-TS-OFFSET PIC S9(3) COMP-3.
007100      05 ALT-PCB-IND PIC X(1).
007200*
007300   01 DB-PCB .
007400      05 DB-PCB-DBNAME PIC X(8).
007500      05 DB-PCB-SEG-LEVEL-NO PIC X(2).
007600      05 DB-PCB-STATUS-CODE PIC X(2).
007700      05 DB-PCB-PROCOPT PIC X(4).
007800      05 FILLER PIC X(4).
007900      05 DB-PCB-SEGNAME PIC X(8).
008000      05 DB-KEY-LENGTH PIC S9(5) COMP.
008100      05        PIC S9(5) COMP.
008200      05 DB-KEY-FEEDBACK PIC X(255).
008300*
008400 PROCEDURE DIVISION USING IO-PCB, ALT-PCB, DB-PCB.
008500*
008600 MAIN-RTN.
008700      DISPLAY "COBOL PROGRAM SQLALL1 EXECUTION BEGINS... ".
008800*
008900      EXEC SQLIMS
009000        WHENEVER SQLERROR GOTO DB-SQL-ERROR
009100      END-EXEC.
009200*
009300      EXEC SQLIMS
009400        WHENEVER SQLWARNING GOTO DB-SQL-WARNING
009500      END-EXEC.
009600*
009700      EXEC SQLIMS
009800        WHENEVER NOT FOUND CONTINUE
009900      END-EXEC.
010000*
010100      EXEC SQLIMS
010200        DECLARE TELE1 CURSOR FOR DYSQL
010300      END-EXEC.
010400*
010500      MOVE "SELECT LASTNAME, FIRSTNAME FROM PCB01.PHONEBOOK"
010600      TO SQL-STMT-TEXT.
010700      MOVE 198 TO SQL-STMT-LENGTH.
010800*
010900      EXEC SQLIMS
011000        PREPARE DYSQL FROM :SQL-STATEMENT
011100      END-EXEC.
011200      MOVE 10 TO SQLIMSN.
011300      EXEC SQLIMS
011400        DESCRIBE OUTPUT DYSQL INTO :SQLIMSDA
011500      END-EXEC.
011600      PERFORM DISPLAY-SQLDA.
011700*
011800      EXEC SQLIMS
011900        OPEN TELE1
012000      END-EXEC.
012100      COMPUTE DBD-COUNT = 0.
012200
012300      PERFORM FETCH-CUSTROOT 10 TIMES.
012400*
012500      DISPLAY "ABOUT TO PERFORM CLOSE TELE1".
012600      EXEC SQLIMS
012700        CLOSE TELE1
012800      END-EXEC.
012900      PERFORM RETURN-TO-CALLER.
013000
013100 FETCH-CUSTROOT.
013200*
013300      EXEC SQLIMS
013400        FETCH TELE1 INTO :SQL-LASTNAME, :SQL-FIRSTNAME
013500      END-EXEC.
013600      IF SQLIMSCODE EQUAL 0
013700      ADD 1 TO DBD-COUNT
013800      DISPLAY " COUNT ", DBD-COUNT,
013900      ", LASTNM= ", SQL-LASTNAME,
014000      ", FORENM= ", SQL-FIRSTNAME
014100      END-IF.
014200************************************************************
014300* DISPLAY DATA NOT FOUND MESSAGE WHEN DATA NOT FOUND.
014400************************************************************
014500 DATA-NOT-FOUND.
014600
014700      DISPLAY " ----------------------------------------".
014800      DISPLAY " ".
014900      DISPLAY "******NO MORE DATA FOUND******".
015000
015100* MOVE SQLIMSCODE TO SQLIMSCODE-DISP.
015200* DISPLAY "SQLIMSCODE = " SQLIMSCODE-DISP.
015300*
015400************************************************************
015500* DISPLAY ERROR MESSAGE
015600************************************************************
015700 DB-SQL-ERROR.
015800
015900      DISPLAY "ERROR OCCURRED IN WHENEVER SQLIMSERROR".
016000      MOVE SQLIMSCODE TO SQLIMSCODE-DISP.
016100      DISPLAY "SQLIMSCODE = " SQLIMSCODE-DISP.
016200      DISPLAY "SQLIMSERRMC= " SQLIMSERRMC.
016300      DISPLAY "SQLIMSSTATE= " SQLIMSSTATE.
016400      GO TO RETURN-TO-CALLER.
016500
016600************************************************************
016700* DISPLAY ERROR MESSAGE
016800************************************************************
016900 DB-SQL-WARNING.
017000
017100      DISPLAY "ERROR OCCURRED IN WHENEVER SQLWARNING".
017200      MOVE SQLIMSCODE TO SQLIMSCODE-DISP.
017300      DISPLAY "SQLIMSCODE = " SQLIMSCODE-DISP.
017400      DISPLAY "SQLIMSERRMC= " SQLIMSERRMC.
017500      DISPLAY "SQLIMSSTATE= " SQLIMSSTATE.
017600
017700*
017800************************************************************
017900*
018000************************************************************
018100 RETURN-TO-CALLER.
018200*
018300      GOBACK.
018400
018500 DISPLAY-SQLDA.
018600
018700      DISPLAY "SQLIMSDAID|" SQLIMSDAID
018800      "|SQLIMSDABC|" SQLIMSDABC
018900      "|SQLIMSN|" SQLIMSN
019000      "|SQLIMSD|" SQLIMSD.
019100      PERFORM WITH TEST AFTER
019200        VARYING LOOP-COUNT FROM 1 BY 1
019300        UNTIL LOOP-COUNT > SQLIMSD
019400        DISPLAY
019500        " ", LOOP-COUNT,
019600        " TYPE=", SQLIMSTYPE(LOOP-COUNT),
019700        " LEN=" , SQLIMSLEN(LOOP-COUNT),
019800        " NAMEL=", SQLIMSNAMEL(LOOP-COUNT),
019900        " NAMEC=" SQLIMSNAMEC(LOOP-COUNT),
020000        "|"
020100      END-PERFORM.
020200
020300 END PROGRAM "SQLALL1".
